plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java'
}

group = 'com.gmail.furkanaxx34'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    // Dependencies
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'

    // Spigot-API
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'

    // annotations
    compileOnly 'org.jetbrains:annotations:22.0.0'
    annotationProcessor 'org.jetbrains:annotations:22.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

shadowJar {
    minimize();
    archiveFileName.set("${project.name}.jar")
    relocate 'co.aikar.commands', 'com.gmail.furkanaxx34.skyraid.acf'
    relocate 'co.aikar.locales', 'com.gmail.furkanaxx34.skyraid.locales'
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
